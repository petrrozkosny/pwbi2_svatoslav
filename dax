dim_datumy = 
/*
Funkce ADDCOLUMNS() pridava sloupecky do tabulky.
Tabulku jsme si vyrobili za pomoci funkce SELECTCOLUMNS()
*/
ADDCOLUMNS( -- krok 3
-- =============== tabulka vracena funkci SELECTCOLUMNS ===================
    /*
    Funkce SELECTCOLUMNS() vybira sloupce z tabulky, v nasem pripade vybirame sloupec [date]
    a jeho hodnoty nasledne pouzijeme pro sloupec datum
    */
    SELECTCOLUMNS( -- krok 2
        -- Funkce CALENDARAUTO() prochazi datovy model, vsechny datumove sloupce,
        -- a nasledne vytvori souvislou datumovou radu mezi nejmensim a nejvetsim datumem
        -- nazev sloupce, ktery je navratovou hodnotou, je Date
        CALENDARAUTO(), --krok 1
    "datum",
    [Date])
-- =============== konec tabulka vracena funkci SELECTCOLUMNS ===================,

-- ============== sloupecky pridavane funkce ADDCOLUMNS ==============
,"rok",YEAR([datum])
,"kvartal",QUARTER([datum])
,"mesic",MONTH([datum])
-- ============== konec sloupecky pridavane funkce ADDCOLUMNS ==============
)


_________________________________________________________________________________

21 mezirocni rozdil srazek v prvnim ctvrtleti v ruzyne = 
VAR tento_rok = 
    CALCULATE(
        SUM(data[Srazky]),
    dim_datumy[kvartal] = 1,
    dim_lokality[Lokalita] = "RUZYNE")

VAR minuly_rok = 
    CALCULATE(
        SUM(data[Srazky]),
    dim_datumy[kvartal] = 1,
    dim_lokality[Lokalita] = "RUZYNE",
    dim_datumy[rok] = MAX(dim_datumy[rok]) - 1)

RETURN 
tento_rok - minuly_rok


__________________________________________________________________________________________________

35 nejsrazkovejsi mesic = 

-- srazky v danem roce a mesici
VAR srazky = SUM(data[Srazky])


-- maximalni mesicni srazky v danem mesici

VAR _tabulka = 

ADDCOLUMNS(
    ALL(data[rok]),
"mesicni srazky",
CALCULATE(SUM(data[Srazky])))

RETURN
IF(srazky = MAXX(_tabulka,[mesicni srazky]),"GOLD")

______________________________________________________________________________________________________

36 nejmene srazkovy rok = 
-- srazky v jednotlivych bodech vizualu
VAR srazky = SUM(data[Srazky])

VAR _tabulka = 
ADDCOLUMNS(
    ALL(data[rok]),
"rocni_srazky",
CALCULATE(SUM(data[Srazky])))

VAR _min_srazky = MINX(_tabulka,[rocni_srazky])

RETURN 
IF(srazky = _min_srazky,"GRAY")


________________________________________________________________________________________________________

37 maximalni rocni srazky = 

MAXX(
    -- tabulka
    ALL(data[rok]),
    -- sloupec, ve kterem hledame maximalni hodnotu
    CALCULATE(SUM(data[Srazky])))

___________________________________________________________________________________________________________

38 prumerne mesicni srazky = 

AVERAGEX(
    ALL(data[rok],data[mesic]),
CALCULATE(SUM(data[Srazky])))
__________________________________________________________________________________________________________

39 maximalni srazky = max(data[Srazky])

___________________________________________________________________________________________________________

40 roky maximalnich srazek = 

VAR _tabulka = 
ADDCOLUMNS(    
    ALL(data[rok]),
"rocni srazky",
CALCULATE(SUM(data[Srazky])))

VAR _max_hodnota = MAXX(_tabulka,[rocni srazky])

RETURN
CONCATENATEX(
    FILTER(_tabulka,[rocni srazky] = _max_hodnota),data[rok])
